= KinD variant

This folder contains the variant to use when deploying locally using a https://github.com/camptocamp/devops-stack-module-cluster-kind[KinD] cluster and an S3 bucket deployed using the https://github.com/camptocamp/devops-stack-module-minio[MinIO] module of the https://devops-stack.io[DevOps Stack].

== Usage

This module can be declared by adding the following block on your Terraform configuration:

[source,terraform]
----
module "thanos" {
  source = "git::https://github.com/camptocamp/devops-stack-module-thanos//kind?ref=<RELEASE>"

  cluster_name     = module.kind.cluster_name
  argocd_namespace = local.argocd_namespace
  base_domain      = module.kind.base_domain
  cluster_issuer   = local.cluster_issuer

  metrics_storage = {
    bucket_name       = "thanos" # Name given to the bucket
    endpoint          = module.minio.endpoint
    access_key        = "readwrite_user" # Name given to the read-write user created by the module MinIO.
    secret_access_key = module.minio.readwrite_secret_key
  }

  thanos = {
    oidc = module.oidc.oidc
  }

  depends_on = [module.oidc]
}
----

As you can see, a minimum requirement for this module is an S3 bucket that uses a user with read/write permissions, both created in the MinIO module we provide (more information below).

NOTE: There are multiple other options that can be set, as in the other modules, but we will not go into detail as this variant is mainly for test and development purposes. You can check the documentation on the other modules to get an idea.

If there is a need to configure something besides the common settings that we have provided, you can customize the chart's `values.yaml` by adding an Helm configuration as an HCL structure:

[source,terraform]
----
module "thanos" {
  source = "git::https://github.com/camptocamp/devops-stack-module-thanos.git//eks"

  cluster_name            = var.cluster_name
  argocd_namespace        = module.cluster.argocd_namespace
  base_domain             = module.cluster.base_domain
  cluster_issuer          = var.cluster_issuer

  metrics_storage = {
    bucket_name       = "thanos" # Name given to the bucket
    endpoint          = module.minio.endpoint
    access_key        = "readwrite_user" # Name given to the read-write user created by the module MinIO.
    secret_access_key = module.minio.readwrite_secret_key
  }

  thanos = {
    oidc = module.oidc.oidc
  }

  helm_values = [{ # Note the curly brackets here
    thanos = {
      map = {
        string = "string"
        bool   = true
      }
      sequence = [
        {
          key1 = "value1"
          key2 = "value2"
        },
        {
          key1 = "value1"
          key2 = "value2"
        },
      ]
      sequence2 = [
        "string1",
        "string2"
      ]
    }
  }]

  depends_on = [module.argocd_bootstrap]
}
----

=== S3 Bucket

IMPORTANT: You are in charge of creating an S3 bucket for Thanos to store the archived metrics.

We've decided to keep the creation of this bucket outside of this module, mainly because the persistence of the data should not be related to the instantiation of the module itself.

You can create an S3 bucket using the https://github.com/camptocamp/devops-stack-module-minio[MinIO] module by simply adding the bucket name to that module's declaration.

[source,terraform]
----
module "minio" {
  source = "git::https://github.com/camptocamp/devops-stack-module-minio?ref=<RELEASE>"

  cluster_name     = module.kind.cluster_name
  base_domain      = module.kind.base_domain
  argocd_namespace = local.argocd_namespace

  minio_buckets = [
    "thanos", # <--
    "loki",
  ]

  depends_on = [module.ingress, module.cert-manager]
}
----

NOTE: Do not forget that the bucket configuration also needs to be passed to the module `kube-prometheus-stack`.

=== OIDC

NOTE: This module was developed with OIDC in mind.

There is an OIDC proxy container deployed as a sidecar on each pod that has a web interface. Consequently, the `thanos` variable is expected to have a map `oidc` containing at least the Issuer URL, the Client ID, and the Client Secret.

You can pass these values by pointing an output from another module (as above), or by defining them explicitly:

[source,terraform]
----
module "thanos" {
  ...

  thanos = {
    oidc = {
      issuer_url    = "<URL>"
      client_id     = "<ID>"
      client_secret = "<SECRET>"
    }
  }

  ...
}
----

=== Resource Configuration

Since the resource requirements are not the same on every deployment and because the consumed resources also influence the cost associated, we refrained from configuring default resource requirements for the components of Thanos. We did, however, set memory limits for some of the pods (`query`, `storegateway` and `compactor` all have a 1 GB memory limit). These values should be customized as you see fit, although there is not really a need in a test deployment.

== Technical Reference

=== Dependencies

==== `module.oidc`

This module needs an OIDC provider to function and consequently it must be one deployed after the module `oidc`.

==== `module.minio`

This module needs to have the configuration for the S3 bucket and consequently it must be one deployed after the module `minio`.

// BEGIN_TF_DOCS
// END_TF_DOCS

=== Reference in table format 

.Show tables
[%collapsible]
====
// BEGIN_TF_TABLES
// END_TF_TABLES
====
